global proc edgeToExtrude(string $curve,string $objName)
{
    //get positions of CVs
    $cv0 = `xform -q -t ($curve+".cv[0]")`;
    $cv1 = `xform -q -t ($curve+".cv[1]")`;
    //get distance between CVs for circle scale
    float $mag=`mag(<<float($cv0[0]),float($cv0[1]),float($cv0[2])>>-<<float($cv1[0]),float($cv1[1]),float($cv1[2])>>)`;
    //make circle
    $circle = `circle -n ($objName+"_profile") -c 0 0 0 -nr 0 1 0 -sw 360 -r 1 -d 3 -ut 0 -tol 0.01 -s 8 -ch 1`;
    $locator = `spaceLocator -p 0 0 0`;
    //position curve
    xform -t $cv0[0] $cv0[1] $cv0[2] $circle;
    xform -s $mag $mag $mag $circle;
    xform -t $cv1[0] $cv1[1] $cv1[2] $locator;
    //aim circle up curve
    $constraint = `aimConstraint -offset 0 0 0 -weight 1 -aimVector 0 -1 0 -upVector 0 1 0 -worldUpType "vector" -worldUpVector 0 1 0 $locator $circle`;
    delete $constraint;
    delete $locator;
    //extrude geo
    extrude -n ($objName+"_extrude") -ch true -rn false -po 1 -et 2 -ucp 0 -fpt 0 -upn 1 -rotation 0 -scale 1 -rsp 1 $circle[0] $curve ;

}

global proc edgeToCurve(int $makeGEO){
	string $sel[] = `ls -sl -flatten`;

	string $array[] = {};

	for( $item in $sel)
	{
	    $curve = `duplicateCurve -ch 1 -rn 0 -local 0  $item` ;
	    
	    $array[size($array)] = $curve[0];
	    
	    print ($curve[0] + "\n");
	};
	select -cl;

	for( $item in $array)
	{
	    select -tgl $item;
	};

	string $selCurves[] = `ls -sl`;
	//get object name
	string $objectName[];
	tokenize $sel[0] "." $objectName;
	
	$newCurve = `attachCurve -n ($objectName[0]+"_path") -ch 1 -rpo 0 -kmk 1 -m 1 -bb 0.5 -bki 0 -p 0.1 $selCurves` ;

	rebuildCurve -ch 1 -rpo 1 -rt 0 -end 1 -kr 1 -kcp 1 -kep 1 -kt 0 -s 4 -d 3 -tol 0.01 $newCurve;

	DeleteHistory;

	CenterPivot;

	for( $item in $selCurves)
	{
	    delete $item;
	}
	if($makeGEO == 1)
	{
	    edgeToExtrude($newCurve[0],$objectName[0]);
	}
	
}
//edgeToCurve(1);